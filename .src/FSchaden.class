' Gambas class file

Private $isClose As Boolean
Private $fmain As FMain
Private $edit As Boolean
Private $s As Settings
Private $Checkboxes As CheckBox[]
Private $DMGmod As Integer

Public Sub Button1_Click()
  
  Dim i As Integer
  
  i = SpinBox1.Value
  i += ValueBox1.Value
  i += $DMGmod
  If CheckBox5.Value Then
    i *= ValueBox2.Value
  Endif
  
  TextLabel4.Text = "Schaden ist: " & i
  
End

Public Sub setcrit()
  
  CheckBox5.Value = True
  
End

Public Sub unsetcrit()
  
  CheckBox5.Value = False
  
End

Public Sub setWisCrit(wis As Integer, crit As Integer)
  
  ValueBox1.Value = wis
  ValueBox2.Value = crit
  
End

Public Sub getWisCrit() As Integer[]
  
  Return [ValueBox1.Value, ValueBox2.Value]
  
End

Public Sub Form_Close()
  
  $isClose = True
  $fmain.Close
  
End

Public Sub isClose() As Boolean
  
  Return $isClose
  
End

Public Sub setFmain(fmain As FMain)
  
  $fmain = fmain
  
End

Public Sub setEdit()
  
  $edit = True
  
End

Public Sub setSetting(sSettings As Settings)
  
  $s = sSettings
  
End

Public Sub LoadForm()
  
  Dim sjson As String
  Dim saCheckboxes As String[][]
  Dim sarray As String[]
  Dim cb As CheckBox
  Dim i As Integer
  
  $Checkboxes = New CheckBox[]
  
  ValueBox1.Value = $s["Schaden/AttributesMod", $s["Schaden/WisMod", 0]]
  ValueBox2.Value = $s["Schaden/CritMod", 1]
  TextLabel2.Text = $s["Schaden/Attribute", "Wis"]
  sjson = $s["Schaden/CheckBoxes", "[[\"Hutz Effekt\",2],[\"PBS\",5],[\"Spitzer Pfeil\",1],[\"Sehr Spitzer Pfeil\",2]]"]
  saCheckboxes = JSON.FromString(sjson)
  
  For Each sarray In saCheckboxes
    cb = New CheckBox(ScrollView1) As "CheckBox"
    cb.Text = sarray[0]
    cb.Tag = CInt(sarray[1])
    cb.x = 5
    cb.Y = i
    cb.h = 32
    cb.w = 192
    i += 32
    $Checkboxes.Add(cb)
  Next
  ''[[Name,Tag],[Name,Tag]]
  
End

Public Sub newCB(Arg As String[])
  
  Dim i As Integer
  Dim cb As CheckBox
  
  If $edit Then
    cb = New CheckBox(ScrollView1) As "CheckBox"
    i = 32 * $Checkboxes.Length
    cb.Text = Arg[0]
    cb.Tag = CInt(Arg[1])
    cb.x = 5
    cb.Y = i 
    cb.h = 32
    cb.w = 192
    
    $Checkboxes.Add(cb)
  Endif
  
End

Public Sub rmCB(Arg As String)
  
  Dim cb As CheckBox
  Dim i As Integer
  
  If $edit Then
    For Each cb In $Checkboxes
      cb.Y = i
      If cb.Text = Arg Then 
        $Checkboxes.Remove($Checkboxes.Find(cb))
        cb.Delete
        i -= 32
        
      Endif 
      
      i += 32
    Next
  Endif
  
End

Public Sub CheckBox_Click()
  
  Dim cb As CheckBox
  
  cb = Last
  
  If cb.Value Then
    $DMGmod += CInt(cb.Tag)
  Else 
    $DMGmod -= CInt(cb.Tag)
  Endif
  
End

Public Sub saveCB() As String
  
  Dim a As New String[]
  
  Dim cb As CheckBox
  Dim b As New String[][]
  
  For Each cb In $Checkboxes
    a = New String[]
    a.Add(cb.Text)
    a.Add(cb.Tag)
    b.Add(a)
  Next
  Return JSON.ToString(b)
  
End

Public Sub SaveForms()
  
  $s["Schaden/AttributesMod"] = ValueBox1.Value 
  $s["Schaden/CritMod"] = ValueBox2.Value 
  $s["Schaden/Attribute"] = TextLabel2.Text
  $s["Schaden/CheckBoxes"] = saveCB()
  
End

Public Sub setArtibut(dmg As String)
  
  TextLabel2.Text = dmg
  
End